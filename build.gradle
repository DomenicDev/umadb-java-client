plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.5'
    id "com.vanniktech.maven.publish" version "0.36.0"
}

group = 'io.umadb'
version = '0.2-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.78.0'
    implementation 'io.grpc:grpc-protobuf:1.78.0'
    implementation 'io.grpc:grpc-stub:1.78.0'

    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter:2.0.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        // This pulls the Protobuf compiler version from Maven Central
        artifact = "com.google.protobuf:protoc:3.25.8"
    }
    plugins {
        grpc {
            // The gRPC Java code generator
            artifact = "io.grpc:protoc-gen-grpc-java:1.78.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

mavenPublishing {

    coordinates("io.github.domenicdev", "umadb-java-client", version.toString())

    pom {
        name = "UmaDB Java Client"
        description = "Thin Java client and wrapper for UmaDB's gRPC API"
        inceptionYear = "2026"
        url = "https://github.com/DomenicDev/umadb-java-client"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "domenicdev"
                name = "Domenic Cassisi"
                url = "https://github.com/DomenicDev"
            }
        }
        scm {
            url = "https://github.com/DomenicDev/umadb-java-client"
            connection = "scm:git:git://github.com/DomenicDev/umadb-java-client.git"
            developerConnection = "scm:git:ssh://git@github.com:DomenicDev/umadb-java-client.git"
        }
    }
}
